ruleset GeneralEducationFTE

/* GeneralEdFTE rules */	

rule 244.16.1 
	when {GeneralEducationFTE}.[GeneralEdFTE] > '0'
	and {SpecialEducation}.[PrimaryDisability] exists then
	require years since {PersonalCore}.[DateOfBirth] as of 1-Dec >= '5'
	and years since {PersonalCore}.[DateOfBirth] as of 1-Sep < '26'
	else 'The General Ed FTE is invalid; only Special Education students who are at least 5 years of age (as of Dec 1 of the current school year) and less than 26 years of age (as of Sept 1 of the current school year) are eligible for General Ed FTE.'

rule 244.16.2 
	when {EEM}.[EntityTypeCategory] = '2'
	and {Membership}.[StudentResidency] <> '12' then
	require {GeneralEducationFTE}.[GeneralEdFTE] = 0
	else 'When the student`s Operating District is an ISD (according to EEM), General Ed FTE may only be claimed when the student`s Residency Code is "12".'
	
rule 244.16.3
	when {EEM}.[SFNCloseDate] < AsOfDate then
	require {GeneralEducationFTE}.[GeneralEdFTE] = 0
	else 'The General Ed FTE is invalid because the School closed before the count date or as of date associated with this collection.'
	
rule 244.16.4
	when {EEM}.[SFNOpenDate] > AsOfDate then
	require {GeneralEducationFTE}.[GeneralEdFTE] = 0
	else 'The General Ed FTE is invalid because the School opened after the count date or as of date associated with this collection.'
	
rule 244.16.5
	when {Enrollment}.[EnrollmentDate] > AsOfDate then
	require {GeneralEducationFTE}.[GeneralEdFTE] = '0.00'
	else 'The General Ed FTE is invalid because the Date of Enrollment is later than the count date or as of date associated with this collection.'	
	
rule 244.16.11
	require sum({GeneralEducationFTE}.[GeneralEdFTE],{SpecialEducation}.[Section52FTE,Section53FTE]) <= '1'
	else 'Students may not have a total FTE > 1.00 (general ed, special ed)'
	
rule 244.16.12
	when {GeneralEducationFTE}.[GeneralEdFTE] > '0'
	and {SpecialEducation} does not exist
	and {ProgramParticipation}.[ProgramEligibilityParticipation] <> '9222' then
	require years since {PersonalCore}.[DateOfBirth] as of 1-Dec >= '5'
	and years since {PersonalCore}.[DateOfBirth] as of 1-Sep < '20'
	else 'The General Ed FTE is invalid. Only students who are at least 5 years of age (as of Dec 1 of the current school year) and less than 20 years of age (as of Sept 1 of the current school year) are eligible for General Ed FTE.'

rule 244.16.13
	when {Enrollment}.[DistrictExitStatus] is in ['05','06','20','21'] then
	expect {GeneralEducationFTE}.[GeneralEdFTE] = 0
	else 'You are attempting to claim General Education FTE for a student who has been reported as completing k-12 education and has a cohort status of "Other Completer". Please confirm that this student is eligible for General Education FTE.'
	
rule 244.16.14
	when {Enrollment}.[DistrictExitStatus] is in ['01','02','03','04','40','41'] then
	require {GeneralEducationFTE}.[GeneralEdFTE] = 0
	else 'FTE may not be claimed; the current cohort status indicates he/she has been reported as having graduated with a diploma.'	
	
rule 244.16.15
	when {SchoolDemographics}.[GradeSetting] is in ['20','30'] then
	require {GeneralEducationFTE}.[GeneralEdFTE] = '0'
	else 'If the Grade or Setting is 20 or 30, then the General Ed FTE cannot be greater than 0.00.'
	
rule 244.16.16
	when {GeneralEducationFTE}.[GeneralEdFTE] > '0'
	and {SpecialEducation} does not exist 
	and {ProgramParticipation}.[ProgramEligibilityParticipation] = '9222' then
	require years since {PersonalCore}.[DateOfBirth] as of 1-Dec >= '5'
	and years since {PersonalCore}.[DateOfBirth] as of 1-Sep < '22'
	else 'The General Ed FTE is invalid. Only students who are at least 5 years of age (as of Dec 1 of the current school year) and less than 22 years of age (as of Sept 1 of the current school year) are eligible for General Ed FTE.'
	