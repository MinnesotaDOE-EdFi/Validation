ruleset Section23a


rule 286.1
	when {Section23a} exists then 
	require {GeneralEducationFTE}.[GeneralEdFte] = '00'
	and {SpecialEducation}.[Section52FTE] = '00'
	and {SpecialEducation}.[Section53FTE] = '00'
	else 'Districts cannot submit FTE when submitting a Section 23a claim for a student.'
	
rule 286.2
	when {Section23a} exists
	and {SpecialEducation}.[PrimaryDisability] exists then 
	require years since {PersonalCore}.[DateofBirth] as of 1-Dec >= 5
	and years since {PersonalCore}.[DateofBirth] as of 1-Sep < 26
	else 'When the Section 23a Component is reported, the student must be at least 5 years of age as of December 1 and less than 26 years of age as of September 1.'

rule 286.3
	when {Section23a} exists
	and {SpecialEducation}.[PrimaryDisability] does not exist then 
	require years since {PersonalCore}.[DateofBirth] as of 1-Dec >= 5
	and years since {PersonalCore}.[DateofBirth] as of 1-Sep < 20
	else 'When the Section 23a Component is reported, the student must be at least 5 years of age as of December 1 and less than 20 years of age as of September 1 of the current school year.'
	

/* MonthClaimed rules */	
rule 286.544.1
	require min({Section23a}.[MonthClaimed]) >= YearMonth({Enrollment}.[EnrollmentDate])
	else 'The enrollment date must be prior to the month/year being claimed.'
	
rule 286.544.2
	require {Section23a}.[MonthClaimed] < YearMonth(today)
	else 'MonthClaimed may not be the current or future month. You may only claim a calendar month after the month has ended.'
	
rule 286.544.3
	require {Section23a}.[MonthClaimed] is unique
	else 'The same year/month combination may only be reported once per component.'
	
rule 286.544.4
	require {Section23a}.[MonthClaimed] >= SchoolYearStartMonth(EOYAsOfDate)
	and {Section23a}.[MonthClaimed] <= YearMonth(EOYAsOfDate)
	else 'Claims for this school year must be for the months of July through June. Claims earlier than the preceding July, or later than June, may not be entered in this school year.'
	
rule 286.544.5
	when years since {PersonalCore}.[DateOfBirth] as of 1-Sep >= 20 
	and {SpecialEducation}.[PrimaryDisability] does not exist
	and {ProgramParticipation}.[ProgramEligibilityParticipation] <> '9222' then
	require {Section23a}.[MonthClaimed] <= YearMonth({PersonalCore}.[DateOfBirth])
	else 'The student became 20 years old prior to one or more of the submitted Month Claimed values, and is therefore not eligible.'
	
	
/* ProgramStartDate rules */
rule 286.543.1
	require {Section23a}.[ProgramStartDate] >= {Enrollment}.[EnrollmentDate]
	else 'The Program Start Date cannot be before the district enrollment date.'
	
rule 286.543.2 
	require {Section23a}.[ProgramStartDate] < {Enrollment}.[DistrictExitDate]
	else 'The Program Start Date cannot be on or after the district exit date.'
	
rule 286.543.3
	expect YearMonth({Section23a}.[ProgramStartDate]) <= min({Section23a}.[MonthClaimed])
	else 'The Program Start Date must be on or before the first day of the month being claimed for Section 23a funding.'