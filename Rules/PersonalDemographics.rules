ruleset PersonalDemographics

/* Ethnicity rules */

rule 210.314.1
	require {PersonalDemographics}.[RacialEthnicCode] matches '%1%'
	else 'At least one of the six digits must be the number "1".'
	
rule 210.314.2 
	require {PersonalDemographics}.[RacialEthnicCode] match '[0-6][0-6][0-6][0-6][0-6][0-1]'
	else 'Any digit in the ethnicity code cannot be greater than 6.'
	
rule 210.314.3 
	require {PersonalDemographics}.[RacialEthnicCode] <> '111111'
	else 'RacialEthnicCode cannot contain all 1`s ("111111").'
	
rule 210.314.4
	require {PersonalDemographics}.[RacialEthnicCode] <> '123456'
	else 'RacialEthnicCode cannot be "123456".'

	
/* PersonalDemographicsCity rules */
rule 210.289.1
	when collection is in [FallGeneralCollection,SpringGeneralCollection,EOYGeneralCollection,TeacherStudentDataLink,StudentRecordMaintenance]
	and {HomelessDemographics} does not exist then
	require {PersonalDemographics}.[City] exists
	else 'If the Homeless Demographics component is not submitted, City is required.'	


/* ResidentLEANumber rules */	
rule 210.296.1
	require {EEM}.[ResidentLEANumber] = {PersonalDemographics}.[ResidentLEANumber]
	and {EEM}.[ResidentLEATypeCategory] = '3'
	else 'The Resident LEA Number is not a valid LEA district.'
	
rule 210.296.2 
	when collection is in [FallGeneralCollection,SpringGeneralCollection,EOYGeneralCollection,TeacherStudentDataLink,StudentRecordMaintenance]
	and ({PersonalDemographics}.[State] = 'MI'
	or {ProgramParticipation}.[ProgramEligibilityParticipation] <> '9110' ) then
	require {PersonalDemographics}.[ResidentLEANumber] exists
	else 'ResidentLEANumber is required unless "State" does not equal "MI" or one of the Program Eligibility Participation codes is 9110 - Out of State Resident.'

rule 210.296.3 
	require {PersonalDemographics}.[ResidentLEANumber] = {EEM}.[ResidentLEANumber]
	else 'The Resident LEA Number is invalid.'
	
rule 210.296.4
	require {PersonalDemographics}.[ResidentLEANumber] is not in [82010,84060]
	else 'The Resident LEA Number cannot be "82010" or "84060".'
	
rule 210.296.5
	when {GeneralEducationFTE}.[GeneralEdFTE] > '0'
	or {SpecialEducation}.[Section52FTE] > '0' 
	or {SpecialEducation}.[Section53FTE] > '0' then
	require {PersonalDemographics}.[ResidentLEANumber] exists
	else 'If the sum of General Ed FTE, Section 52 FTE, and Section 53 FTE is greater than 0.00, then the Resident LEA Number cannot be blank.'
	
rule 210.296.6
	require {PersonalDemographics}.[ResidentLEANumber] = {EEM}.[ResidentLEANumber]
	and {EEM}.[Status] = '01'
	else 'Resident LEA Number must be an entity with a status of "open-active" in EEM'	


/* State rules */	
rule 210.209.1
	when collection is in [FallGeneralCollection,SpringGeneralCollection,EOYGeneralCollection,TeacherStudentDataLink,StudentRecordMaintenance]
	and {HomelessDemographics} does not exist then
	require {PersonalDemographics}.[State] exists
	else 'If the Homeless Demographics component is not submitted, State is required.'

	
/* StreetAddress rules */
rule 210.12.1 
	when collection is in [FallGeneralCollection,SpringGeneralCollection,EOYGeneralCollection,TeacherStudentDataLink,StudentRecordMaintenance]
	and {HomelessDemographics} does not exist then
	require {PersonalDemographics}.[StreetAddress] exists
	else 'If the Homeless Demographics component is not submitted, Street Address is required.'	


/* StreetAddress2 rules */	
rule 210.208.1 
	when {PersonalDemographics}.[StreetAddress] does not exist then
	require {PersonalDemographics}.[StreetAddress2] does not exist
	else 'The "StreetAddress2" cannot be submitted if "StreetAddress" is not submitted.'

	
/* StudentResidentCounty rules */
rule 210.301.1 
	when {ECPrograms} exists then
	require {PersonalDemographics}.[StudentResidentCounty] exists
	else 'If ECPrograms component is submitted, StudentResidentCounty must contain a valid code.'


/* ZipCode rules */	
rule 210.312.1 
	when collection is in [FallGeneralCollection,SpringGeneralCollection,EOYGeneralCollection,TeacherStudentDataLink,StudentRecordMaintenance]
	and {HomelessDemographics} does not exist then
	require {PersonalDemographics}.[ZipCode] exists
	else 'If the Homeless Demographics component is not submitted, ZipCode is required.'
	
rule 210.312.2 
	require {PersonalDemographics}.[ZipCode] match '[0-9][0-9][0-9][0-9][0-9]'
	or {PersonalDemographics}.[ZipCode] match '[0-9][0-9][0-9][0-9][0-9][-][0-9][0-9][0-9][0-9]'
	else 'Zip code must follow the standard five or nine digit format.'
	
