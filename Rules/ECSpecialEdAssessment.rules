ruleset ECSpecialEdAssessment

rule 247.1
	when {ECSpecialEdAssessment} exists then
	require {SpecialEducation} exists
	and {SpecialEducation}.[PrimaryDisability] exists
	else 'The EC Special Ed Assessment component may only be submitted if the Special Ed Component is submitted and PrimaryDisability contains a valid code.'
	
rule 247.2 
	when {ECSpecialEdAssessment} exists then
	require {ECSpecialEdAssessment}.[EntryAssessmentDate] exists 
	or {ECSpecialEdAssessment}.[ExitAssessmentDate] exists 
	else 'Either Entry Assessment Date or Exit Assessment Date is required when the EC Special Ed Assessement component is included in the student record.'
	
rule 247.3
	when count({ECSpecialEdAssessment}) > '1' then
	require ({ECSpecialEdAssessment}.[EntryAssessmentDate] exists and {ECSpecialEdAssessment}.[ExitAssessmentDate] does not exist)
	or ({ECSpecialEdAssessment}.[ExitAssessmentDate] exists and {ECSpecialEdAssessment}.[EntryAssessmentDate] does not exist)
	else 'When multiple EC Special Ed Assessment components are submitted, Entry Assessment Date is required in one component and Exit Assessment Date in that same component must be blank.  In the other component, Exit Assessment Date is required and Entry Assessment Date must be blank.'
	

/* AssessmentTool rules */	
rule 247.413.1
	when {ECSpecialEdAssessment}.[AssessmentTool] is in ['09','10','11','12','13','14','15','16','17','18','19','20','21'] then
	expect {ECSpecialEdAssessment}.[ExitAssessmentDate] does not exist
	else 'A Part C Exit Assessment may be used as an entrance assessment for students transitioning to Part B.  It is not used as a Part B exit assessment.'
	
rule 247.413.2 
	when {ECSpecialEdAssessment}.[AssessmentTool] <> '18' then
	require {ECSpecialEdAssessment}.[OtherToolComments] does not exist
	else 'When Assessment Tool 18 is not selected, Other Tool Comments must be blank.'
	
rule 247.413.3
	when {ECSpecialEdAssessment}.[AssessmentTool] = '18' then
	require {ECSpecialEdAssessment}.[OtherToolComments] exists
	else 'When Assessment Tool 18 is selected, Other Tool Comments are required.'
	
rule 247.413.4 
	when {ECSpecialEdAssessment}.[AssessmentTool] is in ['09','10','11','12','13','14','15','16','17','18','19','20','21'] then
	expect {ECSpecialEdAssessment}.[Outcome1B] does not exist
	and {ECSpecialEdAssessment}.[Outcome2B] does not exist
	and {ECSpecialEdAssessment}.[Outcome3B] does not exist
	else 'When outcome B is selected, assessment tool cannot be 09 - 21.'
	

/* EntryAssessmentDate rules */	
rule 247.415.1
	when {ECSpecialEdAssessment}.[EntryAssessmentDate] exists then
	expect months since {PersonalCore}.[DateOfBirth] as of {ECSpecialEdAssessment}.[EntryAssessmentDate] >= '30'
	and months since {PersonalCore}.[DateOfBirth] as of {ECSpecialEdAssessment}.[EntryAssessmentDate] <= '66'
	else 'Child`s age at the time of assessment is outside of the expected range (2 years 6 months to 5 years 6 months). Please confirm Entry Assessment Date and Child`s Date of Birth are correct.'
	
rule 247.415.2 
	when {ECSpecialEdAssessment}.[EntryAssessmentDate] exists then
	require {ECSpecialEdAssessment}.[ExitAssessmentDate] does not exist
	else 'If Entry Assessment Date is submitted, Exit Assessment Date must be blank within the same component.'


/* ExitAssessmentDate rules */	
rule 247.416.1
	when {ECSpecialEdAssessment}.[ExitAssessmentDate] exists then
	expect years since {PersonalCore}.[DateOfBirth] as of {ECSpecialEdAssessment}.[ExitAssessmentDate] >= '3'
	and months since {PersonalCore}.[DateOfBirth] as of {ECSpecialEdAssessment}.[ExitAssessmentDate] <= '73'
	else 'Child`s age at the time of assessment is outside of the expected range (3 years to 6 years 1 month). Please confirm Exit Assessment Date and Child`s Date of Birth are correct.'
	
rule 247.416.2 
	when {ECSpecialEdAssessment}.[ExitAssessmentDate] exists then
	require {ECSpecialEdAssessment}.[EntryAssessmentDate] does not exist
	else 'If Exit Assessment Date is submitted, Entry Assessment Date must be blank within the same component.'
	
rule 247.416.3
	when {ECSpecialEdAssessment} exists then
	require {ECSpecialEdAssessment}.[ExitAssessmentDate] exist
	or {SchoolDemographics}.[GradeSetting] = '30'
	else 'If grade or setting is not 30, then exit assessment must be reported in at least one component.'
	
rule 247.416.4 
	when {ECSpecialEdAssessment}.[ExitAssessmentDate] exists then
	require {ECSpecialEdAssessment}.[Outcome1B] exists
	else 'A value must be submitted in Outcome 1B when the Exit Assessment Date is reported.'
	
rule 247.416.5
	when {ECSpecialEdAssessment}.[ExitAssessmentDate] exists then
	require {ECSpecialEdAssessment}.[Outcome2B] exists
	else 'A value must be submitted in Outcome 2B when the Exit Assessment Date is reported.'
	
rule 247.416.6
	when {ECSpecialEdAssessment}.[ExitAssessmentDate] exists then
	require {ECSpecialEdAssessment}.[Outcome3B] exists
	else 'A value must be submitted in Outcome 3B when the Exit Assessment Date is reported.'


/* Outcome1B rules */	
rule 247.418.1
	when {ECSpecialEdAssessment}.[Outcome1B] exists then
	require {ECSpecialEdAssessment}.[ExitAssessmentDate] exists
	else 'If outcome 1B is selected then exit assessment date is required.'


/* Outcome2B rules */		
rule 247.420.1
	when {ECSpecialEdAssessment}.[Outcome2B] exists then
	require {ECSpecialEdAssessment}.[ExitAssessmentDate] exists
	else 'If outcome 2B is selected then exit assessment date is required.'


/* Outcome3B rules */		
rule 247.422.1
	when {ECSpecialEdAssessment}.[Outcome3B] exists then
	require {ECSpecialEdAssessment}.[ExitAssessmentDate] exists
	else 'If outcome 3B is selected then exit assessment date is required.'



	
	
	
	


	

